pkgname=sensors-monitor-git
_pkgname=${pkgname%-git}
pkgver=1
pkgrel=1
pkgdesc="A terminal-based system sensors monitor with color-coded output"
arch=('any')
url="https://github.com/bobi/sensors-monitor"
license=('MIT')
depends=('lm_sensors')
makedepends=('cargo' 'sed' 'grep' 'git')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')
install="${_pkgname}.install"

pkgver() {
  cd "${srcdir}/${_pkgname}"

  cargo_ver=$(grep '^version =' Cargo.toml | sed -E 's/version = "(.*)"/\1/')
  git rev-parse --short HEAD | sed "s/^/${cargo_ver}./"
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_pkgname}"
  cargo build --release --frozen
}

# check() {
#   cd "${srcdir}/${_pkgname}"
#   cargo test --frozen
# }

package() {
  cd "${srcdir}/${_pkgname}"
  install -Dm 755 "target/release/${_pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm 644 "sensors-monitor.conf" "${pkgdir}/usr/share/${_pkgname}/sensors-monitor.conf"
}
